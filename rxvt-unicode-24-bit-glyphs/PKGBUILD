# Maintainer: mrdotx <klassiker@gmx.de>
_pkgname=rxvt-unicode
pkgname=rxvt-unicode-24-bit-glyphs
pkgver=9.22
pkgrel=1
pkgdesc='Unicode enabled rxvt-clone terminal emulator (urxvt) with truecolor and glyphs'
arch=('i686' 'x86_64')
url='http://software.schmorp.de/pkg/rxvt-unicode.html'
license=('GPL')
depends=('libxft' 'perl' 'startup-notification' 'rxvt-unicode-terminfo')
provides=(rxvt-unicode)
conflicts=(rxvt-unicode)
source=(http://dist.schmorp.de/rxvt-unicode/$_pkgname-$pkgver.tar.bz2
    'urxvt.desktop'
    'font-width-fix.patch'
    '24-bit-color.patch'
    'line-spacing-fix.patch'
    'sgr-mouse-mode.patch'
    'enable-wide-glyphs.patch'
    'perl-segfault.patch'
    )
sha1sums=('e575b869782fbfed955f84f48b204ec888d91ba1'
          'b5a4507f85ebb7bac589db2e07d9bc40106720d9'
          '01ee8f212add79a158dcd4ed78d0ea1324bdc59b'
          '9f5db305863b17175e72f9e2d4a78d0d7f25fd34'
          'b7fde1c46af45e831828738874f14b092b1e795f'
          'dfbc8729c545105eff21e20ef3a4a3841a68a192'
          'd9f3b52b273d13fbd4089a7869ab2e70bf190bc9'
          '361f4649596342d07311c2590b809b85b3a918ee')

prepare() {
    cd $_pkgname-$pkgver

    patch -p0 -i ../font-width-fix.patch
    patch -p0 -i ../24-bit-color.patch
    patch -p0 -i ../line-spacing-fix.patch
    patch -p0 -i ../sgr-mouse-mode.patch
    patch -p1 -i ../enable-wide-glyphs.patch
    patch -p1 -i ../perl-segfault.patch
}

build() {
    cd $_pkgname-$pkgver

    # do not specify --with-terminfo (FS#46424)
    ./configure \
        --prefix=/usr \
        --enable-24-bit-color \
        --enable-256-color \
        --enable-combining \
        --enable-fading \
        --enable-font-styles \
        --enable-iso14755 \
        --enable-keepscrolling \
        --enable-lastlog \
        --enable-mousewheel \
        --enable-next-scroll \
        --enable-perl \
        --enable-pointer-blank \
        --enable-rxvt-scroll \
        --enable-selectionscrolling \
        --enable-slipwheeling \
        --enable-smart-resize \
        --enable-startup-notification \
        --enable-transparency \
        --enable-unicode3 \
        --enable-utmp \
        --enable-wide-glyphs \
        --enable-wtmp \
        --enable-xft \
        --enable-xim \
        --enable-xterm-scroll
    make
}

package() {
    # install freedesktop menu
    install -Dm644 urxvt.desktop "$pkgdir/usr/share/applications/urxvt.desktop"

    cd $_pkgname-$pkgver

    # workaround terminfo installation
    export TERMINFO="$srcdir/terminfo"
    install -d "$TERMINFO"
    make DESTDIR="$pkgdir" install
}
